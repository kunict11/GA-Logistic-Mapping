dim 110 110
ang_picture 0 0 110 110
ang_origin 4 4
ang_drawsystem_p 2 1 1 2 3
linethickness 0.4
point O 0 0
point R 0 0 2.83 2.83
point P 10 10
distance r O R

% for printing the current value of r
point L 14 94
point V 22 94
printat L {r = }
printvalueat_l V r

color 0 255 0
drawline O  P

number scale 10
color 0 0 255
ang_draw_parametric_curve x {0;x<scale;x+0.001}{scale*x;scale*r*x*(1-x)}

color 0 0 0
ang_point X 2 0

number scale 10
number x_expr 0.2
expression y {r*x_expr*(1-x_expr)}
expression y2 {r*y*(1-y)}
color 255 0 0
expression ax {scale*x_expr}
expression ay {scale*y}
expression by {scale*y2}
ang_point A ax ay
drawsegment X A
ang_point B ay ay
drawsegment A B
ang_point A ay by
drawsegment B A
number count 0
while { count<=200 }
{
    number y y2
    expression y2 {r*y*(1-y)}
    expression ay {scale*y}
    expression by {scale*y2}
    ang_point B ay ay
    drawsegment A B
    ang_point A ay by
    drawsegment B A

    expression count {count+1}
}
animation_frames 200 60
